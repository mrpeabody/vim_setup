" Make Vim more useful
set nocompatible
set smoothscroll

" allow custom local vim configs with limited options
set exrc
set secure

" Styled and colored underline support
let &t_AU = "\e[58:5:%dm"
let &t_8u = "\e[58:2:%lu:%lu:%lum"
let &t_Us = "\e[4:2m"
let &t_Cs = "\e[4:3m"
let &t_ds = "\e[4:4m"
let &t_Ds = "\e[4:5m"
let &t_Ce = "\e[4:0m"
" Strikethrough
let &t_Ts = "\e[9m"
let &t_Te = "\e[29m"
" Truecolor support
let &t_8f = "\e[38:2:%lu:%lu:%lum"
let &t_8b = "\e[48:2:%lu:%lu:%lum"
let &t_RF = "\e]10;?\e\\"
let &t_RB = "\e]11;?\e\\"
" Bracketed paste
let &t_BE = "\e[?2004h"
let &t_BD = "\e[?2004l"
let &t_PS = "\e[200~"
let &t_PE = "\e[201~"

let g:polyglot_disabled = ['markdown', 'c', 'cpp']

call plug#begin('~/.vim/plugged')

Plug 'neoclide/coc.nvim', {'branch': 'release'}
Plug 'josa42/vim-lightline-coc'
Plug 'scrooloose/nerdtree', { 'on':  'NERDTreeToggle' }
Plug 'Xuyuanp/nerdtree-git-plugin', { 'on':  'NERDTreeToggle' }
Plug 'tpope/vim-fugitive'
Plug 'christoomey/vim-tmux-navigator'
Plug 'junegunn/gv.vim'
Plug 'hynek/vim-python-pep8-indent', { 'for': 'python' }
Plug 'tell-k/vim-autopep8', { 'for': 'python' }
Plug 'raimondi/delimitmate'
Plug 'yuezk/vim-js'
Plug 'sheerun/vim-polyglot'
Plug 'othree/csscomplete.vim'
Plug 'vitiral/MatchTagAlways', { 'branch': 'rawstring' }
Plug 'lepture/vim-jinja'
Plug 'SirVer/ultisnips'
Plug 'tpope/vim-markdown', { 'for': 'markdown' }
Plug 'suan/vim-instant-markdown', { 'for': 'markdown' }
Plug 'Yggdroot/indentLine'
Plug 'terryma/vim-multiple-cursors'
Plug 'nacitar/a.vim'
Plug 'rdolgushin/gitignore.vim'
Plug 'matze/vim-move'
Plug 'rhysd/clever-f.vim'
Plug 'ctrlpvim/ctrlp.vim'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-repeat'
Plug 'AndrewRadev/splitjoin.vim'
Plug 'tommcdo/vim-lion'
" Plug 'fatih/vim-go', { 'do': ':GoUpdateBinaries' }
" Plug 'rust-lang/rust.vim'

" Snippets are separated from the engine. Add this if you want them:
Plug 'honza/vim-snippets'
Plug 'airblade/vim-gitgutter', { 'branch': 'main' }
Plug 'tpope/vim-surround'
Plug 'foosoft/vim-argwrap', { 'on': 'ArgWrap' }
Plug 'itchyny/lightline.vim'
Plug 'majutsushi/tagbar', { 'on': 'TagbarToggle' }
Plug 'djoshea/vim-autoread'
Plug 'milkypostman/vim-togglelist'

" Extra colorschemes (optional -- comment if not needed)
Plug 'morhetz/gruvbox'
Plug 'mhartington/oceanic-next'
Plug 'tomasiser/vim-code-dark'
Plug 'ayu-theme/ayu-vim'
Plug 'jacoborus/tender.vim'
Plug 'sonph/onehalf', { 'rtp': 'vim' }
Plug 'patstockwell/vim-monokai-tasty'
Plug 'sainnhe/everforest'
Plug 'cocopon/iceberg.vim'

" Conceal and enable ANSI escape symbols
Plug 'lambdalisue/vim-pager'
Plug 'powerman/vim-plugin-AnsiEsc', { 'on': 'AnsiEsc' }

" Comment out the below plugin in no patched font is available
Plug 'ryanoasis/vim-devicons'

" All of your Plugins must be added before the following line
call plug#end()            " required


" Color scheme for both VIM and the bottom status panel. Options are:
" - gruvbox            (most popular)
" - OceanicNext        (my personal favorite, default)
" - everforest         (excellent theme for long coding sessions)
" - codedark           (second best -- mimics the default VSCode theme)
" - ayu                (very nice super dark theme)
" - tender             (24-bit soft dark colorscheme, also has a terminal theme available)
" - onehalfdark        (a nice clean dark theme)
" - iceberg            (a well-designed, bluish color scheme)
" - vim-monokai-tasty  (clone of sublime text editor theme)
let g:COLORSCHEME = 'everforest'


" Modify g:font_patched to 0 if no patched font is installed
let g:font_patched = 1

" Multiple cursors setup
nnoremap <silent> <C-g> :MultipleCursorsFind <C-R>/<CR>
vnoremap <silent> <C-g> :MultipleCursorsFind <C-R>/<CR>


" CoC completion engine settings

let g:coc_global_extensions = [
            \    'coc-json',
            \    'coc-eslint',
            \    'coc-tsserver',
            \    'coc-css',
            \    'coc-cmake',
            \    'coc-clangd',
            \    'coc-pyright',
            \    'coc-snippets',
            \    'coc-git',
            \    'coc-sh',
            \    'coc-vimlsp'
            \]

" Use <TAB> and <S-TAB> to navigate completion menu
inoremap <silent><expr> <TAB> pumvisible() ? "\<C-n>" : "\<TAB>"
inoremap <silent><expr> <S-TAB> pumvisible() ? "\<C-p>" : "\<S-TAB>"

" fix some highlights
autocmd ColorScheme * hi! link PmenuSel Visual

" Use <c-space> to trigger completion
inoremap <silent><expr> <c-space> coc#refresh()

" Highlight the symbol and its references when holding the cursor
autocmd CursorHold * silent call CocActionAsync('highlight')

" " Enable some IDE-like features for JS / TS, Python, and Java
autocmd FileType java,javascript,jsx,typescript,tsx,python,cpp,cs nmap <silent> <Leader>d :call CocActionAsync('doHover')<CR>
autocmd FileType java,javascript,jsx,typescript,tsx,python,cpp,cs nmap <silent> gd <Plug>(coc-definition)
autocmd FileType java,javascript,jsx,typescript,tsx,python,cpp,cs nmap <silent> gi <Plug>(coc-implementation)
autocmd FileType java,javascript,jsx,typescript,tsx,python,cpp,cs nmap <silent> gr <Plug>(coc-references)
autocmd FileType java,javascript,jsx,typescript,tsx,python,cpp,cs nmap <silent> <Leader>e <Plug>(coc-rename)

" same for Go
autocmd FileType go nmap <silent> gd :GoDef<CR>
autocmd FileType go nmap <silent> <Leader>d :GoDoc<CR>
autocmd FileType go nmap <silent> gr :GoReferrers<CR>
autocmd FileType go nmap <silent> <Leader>e :GoRename<CR>

" Map function and class text objects
" NOTE: Requires 'textDocument.documentSymbol' support from the language server
xmap if <Plug>(coc-funcobj-i)
omap if <Plug>(coc-funcobj-i)
xmap af <Plug>(coc-funcobj-a)
omap af <Plug>(coc-funcobj-a)
xmap ic <Plug>(coc-classobj-i)
omap ic <Plug>(coc-classobj-i)
xmap ac <Plug>(coc-classobj-a)
omap ac <Plug>(coc-classobj-a)


" Git Gutter settings
if exists('&signcolumn')  " Vim 7.4.2201
  set signcolumn=yes
else
  let g:gitgutter_sign_column_always = 1
endif
set updatetime=100


" Ultisnips settings
let g:UltiSnipsExpandTrigger='<tab>'
let g:UltiSnipsJumpForwardTrigger='<tab>'
let g:UltiSnipsJumpBackwardTrigger='<s-tab>'
let g:UltiSnipsListSnippets = '<c-b>'
let g:UltiSnipsSnippetsDir=expand("~/.vim/CustomSnippets")
let g:UltiSnipsSnippetDirectories=["CustomSnippets", "UltiSnips"]


" clear search on enter
nnoremap <silent> <CR> :nohlsearch<CR><CR>

" quickfix list improvements
" Go to the previous location
nnoremap [q :cprev<CR>

" Go to the next location
nnoremap ]q :cnext<CR>

function! ToggleQuickFix()
    if empty(filter(getwininfo(), 'v:val.quickfix'))
        copen
    else
        cclose
    endif
endfunction

nnoremap <silent> <leader>q :call ToggleQuickFix()<cr>

" text alignment settings
let g:lion_squeeze_spaces = 1


" Auto-PEP8 settings
let g:autopep8_max_line_length=120
let g:autopep8_disable_show_diff=1
autocmd FileType python noremap <buffer> <F8> :call Autopep8()<CR>


" Clever F settings
let g:clever_f_smart_case = 1
let g:clever_f_across_no_line = 1
let g:clever_f_fix_key_direction = 1


" Polyglot plugin settings
let g:vim_json_syntax_conceal = 0
let g:markdown_syntax_conceal = 0
let g:javascript_plugin_flow = 0
let g:markdown_fenced_languages = ['html', 'python', 'javascript', 'typescript', 'cpp', 'java', 'bash=sh']
let g:jsx_ext_required = 0
let g:javascript_plugin_jsdoc = 1


" Markdown preview plugin
let g:instant_markdown_slow = 0
let g:instant_markdown_autostart = 0
let g:instant_markdown_port = 3456
let g:instant_markdown_autoscroll = 0


" JSX / TSX plugin settings
let g:vim_jsx_pretty_colorful_config = 1


" Go Settings
let g:go_fmt_autosave = 0
let g:go_mod_fmt_autosave = 0
let g:go_asmfmt_autosave = 0
let g:go_metalinter_autosave = 0
let g:go_imports_autosave = 0
autocmd FileType go noremap <buffer> <F8> :GoFmt<CR>

" Jump next/previous diagnostic issue (error/warning/info/hint)
nmap <silent> [e <Plug>(coc-diagnostic-prev)
nmap <silent> ]e <Plug>(coc-diagnostic-next)

" QuickFix / Location list settings
nmap <script> <silent> <leader>l :call ToggleLocationList()<CR>
nmap <script> <silent> <leader>q :call ToggleQuickfixList()<CR>

" go through location lists in general
nmap <silent>]l :lnext<CR>
nmap <silent>[l :lprevious<CR>


" Toggle vim line numbers
nmap <silent> <C-t><C-t> :set invrelativenumber<CR>


au BufNewFile,BufRead *.html,*.htm,*.shtml,*.stm set ft=jinja.html


" Automatically fix according to (ts-)standard
autocmd FileType javascriptreact,javascript,typescript,typescriptreact,tsx,jsx nnoremap <silent> <buffer> <F8> :CocCommand eslint.executeAutofix<CR>

" Use the OS clipboard by default (on versions compiled with `+clipboard`)
if (executable('pbcopy') || executable('xclip') || executable('xsel') || executable('wl-copy')) && has('clipboard')
    set clipboard^=unnamed,unnamedplus
endif


" Enhance command-line completion
set wildmenu


" Allow backspace in insert mode
set backspace=indent,eol,start


" vim indentation settings
let delimitMate_expand_cr = 1
let delimitMate_expand_space = 1
let delimitMate_nesting_quotes = ['"','`']
let delimitMate_expand_inside_quotes = 1
let delimitMate_balance_matchpairs = 1
imap <silent> <BS> <C-R>=CoCOnDeleteChar()<CR><Plug>delimitMateBS

function! CoCOnDeleteChar()
  if pumvisible()
    return "\<C-y>"
  endif
  return ""
endfunction


" Highlight matching parentheses/braces by default
set showmatch


" Optimize for fast terminal connections
set ttyfast


" Add the g flag to search/replace by default
set gdefault

" Use UTF-8 without BOM
set encoding=utf-8

" Change mapleader
let mapleader=","


" Splits config
set splitbelow
set splitright

" reduces splits to a single line
set wmh=0


" resize current buffer by +/- 5
nnoremap <silent> _ :vertical resize -5<cr>
nnoremap <silent> + :vertical resize +5<cr>
nnoremap <silent> - :resize +5<cr>


" Hit f3 in order to have a 'normal' paste in the insert mode
if !has('nvim')
    set pastetoggle=<F3>
endif


" Disable arrow keys in both insert and normal mode
nnoremap <Up> <NOP>
nnoremap <Down> <NOP>
nnoremap <Left> <NOP>
nnoremap <Right> <NOP>
vnoremap <Up> <NOP>
vnoremap <Down> <NOP>
vnoremap <Left> <NOP>
vnoremap <Right> <NOP>
inoremap <Up> <NOP>
inoremap <Down> <NOP>
inoremap <Left> <NOP>
inoremap <Right> <NOP>


" Disable mouse scroll
inoremap <ScrollWheelUp> <NOP>
inoremap <ScrollWheelDown> <NOP>
nmap <ScrollWheelUp> <NOP>
nmap <S-ScrollWheelUp> <NOP>
nmap <C-ScrollWheelUp> <NOP>
nmap <ScrollWheelDown> <NOP>
nmap <S-ScrollWheelDown> <NOP>
nmap <C-ScrollWheelDown> <NOP>
nmap <ScrollWheelLeft> <NOP>
nmap <S-ScrollWheelLeft> <NOP>
nmap <C-ScrollWheelLeft> <NOP>
nmap <ScrollWheelRight> <NOP>
nmap <S-ScrollWheelRight> <NOP>
nmap <C-ScrollWheelRight> <NOP>


" By default don't wrap lines
set nowrap

" But do wrap on these types of files...
autocmd FileType markdown setlocal wrap
autocmd FileType html setlocal wrap


" Enable per-directory .vimrc files and disable unsafe commands in them
set exrc
set secure


" Enable syntax highlighting
syntax on


" A vertical line that delimits 120 chars (with a few exceptions)
set colorcolumn=121
highlight ColorColumn ctermbg=236
au FileType python setl colorcolumn=80
au FileType rst setl colorcolumn=70


" Color scheme
try | set t_Co=256
set background=dark
if (has("termguicolors"))
    set termguicolors
endif

let g:codedark_italics=1
let ayucolor="dark"
let g:oceanic_next_terminal_italic = 1
let g:oceanic_next_terminal_bold = 1
let g:onedark_termcolors=256
let g:one_allow_italics = 1

execute 'colorscheme ' .. g:COLORSCHEME

catch  | endtry


" Highlight current line
set cursorline


" Modify some highlighting
hi SpellBad cterm=undercurl ctermfg=none ctermbg=none gui=undercurl
hi SpellCap cterm=undercurl ctermfg=none ctermbg=none gui=undercurl guisp=#fac863
hi SpellLocal cterm=undercurl ctermfg=none ctermbg=none gui=undercurl guisp=cyan
hi SpellRare cterm=undercurl ctermfg=none ctermbg=none gui=undercurl guisp=darkmagenta


" Enable line numbers
set number


" Search settings

" Highlight searches
set hlsearch

" Ignore case of searches
set ignorecase
set smartcase

" Highlight dynamically as pattern is typed
set incsearch


" Fuzzy search - files/buffers/MRU
let g:ctrlp_map = '<c-p>'
let g:ctrlp_cmd = 'CtrlP'
let g:ctrlp_working_path_mode = 'wa'
set wildignore+=*/node_modules/*,*/tmp/*,*.so,*.swp,*.zip       " MacOSX/Linux
set wildignore+=*\\node_modules\\*,*\\tmp\\*,*.swp,*.zip,*.exe  " Windows

let g:ctrlp_custom_ignore = {
  \ 'dir':  '\v[\/]\.(git|hg|svn)$',
  \ 'file': '\v\.(exe|so|dll)$',
  \ 'link': 'some_bad_symbolic_links'}

let g:ctrlp_user_command = ['.git', 'cd %s && git ls-files -co --exclude-standard']



" Auto pairs setup
let g:AutoPairsFlyMode = 0


" Always show status line
set laststatus=2


" Respect modeline in files
set modeline
set modelines=4


" Disable mouse in all modes
set mouse=


" Disable error bells
set noerrorbells


" Don’t reset cursor to start of line when moving around.
set nostartofline


" Show the cursor position
set ruler


" Don’t show the intro message when starting Vim
set shortmess=atI


" Show the current mode
set showmode


" Show the filename in the window titlebar
set title


" Show the (partial) command as it’s being typed
set showcmd


" Start scrolling three lines before the horizontal window border
set scrolloff=2


" Strip trailing whitespace (,ss)
function! StripWhitespace()
    let save_cursor = getpos(".")
    let old_query = getreg('/')
    :%s/\s\+$//e
    call setpos('.', save_cursor)
    call setreg('/', old_query)
endfunction
noremap <leader>ss :call StripWhitespace()<CR>


" Save a file as root (,W)
noremap <leader>W :w !sudo tee % > /dev/null<CR>


" Automatic commands
set timeoutlen=1000 ttimeoutlen=0
nnoremap <silent> <F6> :!clear;python3 %<CR>
nnoremap <silent> <F5> :!clear;python2 %<CR>


" Filesystem Tree
nmap <silent> <Leader>n  :NERDTreeToggle<CR>
let g:NERDTreeIndicatorMapCustom = {
            \ "Modified"  : "✹",
            \ "Staged"    : "✚",
            \ "Untracked" : "✭",
            \ "Renamed"   : "➜",
            \ "Unmerged"  : "═",
            \ "Deleted"   : "✖",
            \ "Dirty"     : "✗",
            \ "Clean"     : "✔︎",
            \ "Unknown"   : "?"
            \}
let NERDTreeIgnore = ['\.pyc$']
autocmd bufenter * if (winnr("$") == 1 && exists("b:NERDTree") && b:NERDTree.isTabTree()) | q | endif

" Disable autocommenting
" autocmd FileType * setlocal formatoptions-=c formatoptions-=r formatoptions-=o


" Tabs
nnoremap th  :tabfirst<CR>
nnoremap tj  :tabnext<CR>
nnoremap tk  :tabprev<CR>
nnoremap tl  :tablast<CR>
nnoremap tt  :tabedit<Space>
nnoremap tn  :tabnext<Space>
nnoremap tm  :tabm<Space>
nnoremap td  :tabclose<CR>


" fix for very large screens (can't see the cursor)
if has('mouse_sgr')
    set ttymouse=sgr
endif


" Disable Macro recorder
map q <Nop>


" Tagbar (Code structure hotkey)
nmap <silent> <Leader>b  :TagbarToggle<CR>
let g:tagbar_type_typescript = {
  \ 'ctagsbin' : 'tstags',
  \ 'ctagsargs' : '-f-',
  \ 'kinds': [
    \ 'e:enums:0:1',
    \ 'f:function:0:1',
    \ 't:typealias:0:1',
    \ 'M:Module:0:1',
    \ 'I:import:0:1',
    \ 'i:interface:0:1',
    \ 'C:class:0:1',
    \ 'm:method:0:1',
    \ 'p:property:0:1',
    \ 'v:variable:0:1',
    \ 'c:const:0:1',
  \ ],
  \ 'sort' : 0
\ }


" Map uppercase commands as well as lowercase
command WQ wq
command Wq wq
command W w
command Q q


" Folding settings
set nofoldenable


" .swp files location
set directory=$HOME/.vim/swapfiles//


" Neovim Ctrl+H fix
if has('nvim')
    set guicursor=
    nmap <BS> <C-W>h
 endif

" indentation guide character
let g:indentLine_char = '┆'
let g:indentLine_fileTypeExclude = ['cpp', 'c', 'text']


" Vim
" guide color
let g:indentLine_color_term = 239

" "GVim
let g:indentLine_color_gui = '#4e4e4e'


" Arguments/lists/dicts inline/per line
nnoremap <silent> <leader>a :ArgWrap<CR>


" Tab title improvements
set tabline=%!MyTabLine()  " custom tab pages line
function MyTabLine()
	let s = '' " complete tabline goes here
	" loop through each tab page
	for t in range(tabpagenr('$'))
		" set highlight
		if t + 1 == tabpagenr()
			let s .= '%#TabLineSel#'
		else
			let s .= '%#TabLine#'
		endif
		" set the tab page number (for mouse clicks)
		let s .= '%' . (t + 1) . 'T'
		let s .= ' '
		" set page number string
		let s .= t + 1 . ' '
		" get buffer names and statuses
		let n = ''      "temp string for buffer names while we loop and check buftype
		let m = 0       " &modified counter
		let bc = len(tabpagebuflist(t + 1))     "counter to avoid last ' '
		" loop through each buffer in a tab
		for b in tabpagebuflist(t + 1)
			" buffer types: quickfix gets a [Q], help gets [H]{base fname}
			" others get 1dir/2dir/3dir/fname shortened to 1/2/3/fname
			if getbufvar( b, "&buftype" ) == 'help'
				let n .= '[H]' . fnamemodify( bufname(b), ':t:s/.txt$//' )
			elseif getbufvar( b, "&buftype" ) == 'quickfix'
				let n .= '[Q]'
			else
				let n .= pathshorten(bufname(b))
			endif
			" check and ++ tab's &modified count
			if getbufvar( b, "&modified" )
				let m += 1
			endif
			" no final ' ' added...formatting looks better done later
			if bc > 1
				let n .= ' '
			endif
			let bc -= 1
		endfor
		" add modified label [n+] where n pages in tab are modified
		if m > 0
			let s .= '[' . m . '+]'
		endif
		" select the highlighting for the buffer names
		" my default highlighting only underlines the active tab
		" buffer names.
		if t + 1 == tabpagenr()
			let s .= '%#TabLineSel#'
		else
			let s .= '%#TabLine#'
		endif
		" add buffer names
		if n == ''
			let s.= '[New]'
		else
			let s .= n
		endif
		" switch to no underlining and add final space to buffer list
		let s .= ' '
	endfor
	" after the last tab fill with TabLineFill and reset tab page nr
	let s .= '%#TabLineFill#%T'
	" right-align the label to close the current tab page
	if tabpagenr('$') > 1
		let s .= '%=%#TabLineFill#%999Xclose'
	endif
	return s
endfunction


" Devicons settings
let g:WebDevIconsUnicodeDecorateFolderNodes = 1


" Line move plugin
if !has("nvim")
    let c='a'
    while c <= 'z'
        exec "set <A-".c.">=\e".c
        exec "imap \e".c." <A-".c.">"
        let c = nr2char(1+char2nr(c))
    endw
endif

let g:move_key_modifier = 'A'


" Bottom line settings (Lightline plugin)
set noshowmode

let g:separator_left_symbol = g:font_patched ? "\ue0b0":""
let g:separator_right_symbol = g:font_patched?"\ue0b2":""
let g:subseparator_left_symbol = g:font_patched?"\ue0b1":"|"
let g:subseparator_right_symbol = g:font_patched?"\ue0b3":"|"
let g:read_only_symbol = g:font_patched?"\ue0a2":"RO"
let g:git_symbol = g:font_patched?"\ue0a0":""

let g:lightline#coc#indicator_warnings = g:font_patched?"\uf071 ":"*"
let g:lightline#coc#indicator_errors = "X "
let g:lightline#coc#indicator_info = g:font_patched?"\uf05a ":"i"
let g:lightline#coc#indicator_hints = g:font_patched?"\uf432 ":"~"

let lightline_theme_map = {
    \ 'OceanicNext': 'oceanicnext2',
    \ 'ayu': 'ayu_dark',
    \ 'vim-monokai-tasty': 'monokai_tasty',
\ }

let g:lightline = {
      \ 'colorscheme': get(lightline_theme_map, g:COLORSCHEME, g:COLORSCHEME),
      \ 'mode_map': { 'c': 'NORMAL' },
      \ 'active': {
      \   'left': [['mode', 'paste'], ['fugitive', 'filename']],
      \   'right': [['coc_errors', 'coc_warnings', 'coc_hints', 'coc_info', 'lineinfo', 'percent'], [ 'fileformat', 'fileencoding', 'filetype' ]]
      \ },
      \ 'inactive': {
      \   'left': [['filename']],
      \   'right': [['lineinfo', 'percent']]
      \ },
      \ 'component_function': {
      \   'lineinfo': 'LightLineLineinfo',
      \   'modified': 'LightLineModified',
      \   'readonly': 'LightLineReadonly',
      \   'fugitive': 'LightLineFugitive',
      \   'filename': 'LightLineFilename',
      \   'fileformat': 'LightLineFileformat',
      \   'filetype': 'LightLineFiletype',
      \   'fileencoding': 'LightLineFileencoding',
      \   'mode': 'LightLineMode'
      \ },
      \ 'component_expand': {
      \   'coc_warnings': 'lightline#coc#warnings',
      \   'coc_errors': 'lightline#coc#errors',
      \   'coc_info': 'lightline#coc#info',
      \   'coc_hints': 'lightline#coc#hints'
      \ },
      \ 'component_type': {
      \   'coc_warnings': 'warning',
      \   'coc_errors': 'error',
      \   'coc_info': 'info',
      \   'coc_hints': 'warning',
      \ },
      \ 'separator': { 'left': g:separator_left_symbol, 'right': g:separator_right_symbol},
      \ 'subseparator': { 'left':g:subseparator_left_symbol, 'right': g:subseparator_right_symbol}
      \ }

function! LightLineLineinfo()
    return winwidth(0) > 50 ? line('.').':'.virtcol('.') : ''
endfunction

function! LightLineModified()
  return &ft =~ 'help\|vimfiler\|gundo' ? '' : &modified ? '+' : &modifiable ? '' : '-'
endfunction

function! LightLineReadonly()
  return &readonly ? g:read_only_symbol : ''
endfunction

function! LightLineFilename()
  let fname = winwidth(0) > 120 ? expand('%:f') : expand('%:t')
  return fname == 'ControlP' && has_key(g:lightline, 'ctrlp_item') ? g:lightline.ctrlp_item :
        \ fname =~ '__Tagbar__' ? '' :
        \ fname =~ 'NERD_tree' ? '' :
        \ &ft == 'vimfiler' ? vimfiler#get_status_string() :
        \ &ft == 'unite' ? unite#get_status_string() :
        \ &ft == 'vimshell' ? vimshell#get_status_string() :
        \ ('' != LightLineReadonly() ? LightLineReadonly() . ' ' : '') .
        \ ('' != fname ? fname : '[No Name]') .
        \ ('' != LightLineModified() ? ' ' . LightLineModified() : '')
endfunction

function! LightLineFugitive()
  try
    if expand('%:t') !~? 'Tagbar\|Gundo\|NERD' && &ft !~? 'vimfiler' && exists('*FugitiveHead')
      let branch = FugitiveHead()
      let gitStats = GitGutterGetHunkSummary()
      return winwidth(0) > 100 ?
          \ (branch !=# '' ? '+'.gitStats[0].' ~'.gitStats[1].' -'.gitStats[2].' '.g:git_symbol.' '.branch : '') :
          \ (winwidth(0) > 80 ? (branch !=# '' ? g:git_symbol.' '.branch : ''): '')
    endif
  catch
  endtry
  return ''
endfunction

function! LightLineFiletype()
    return winwidth(0) > 50 ? (g:font_patched ? WebDevIconsGetFileTypeSymbol().' ' : winwidth(0) > 100 ? &filetype : '') : ''
endfunction

function! LightLineFileformat()
  return winwidth(0) > 100 ? (&fileformat !=# '' ? (g:font_patched ? WebDevIconsGetFileFormatSymbol().' ' : &fileformat) : '') : ''
endfunction

function! LightLineFileencoding()
  return winwidth(0) > 100 ? (&fenc !=# '' ? &fenc : &enc) : ''
endfunction

function! LightLineMode()
  let fname = expand('%:t')
  return fname =~ '__Tagbar__' ? 'Structure' :
        \ fname == 'ControlP' ? 'CtrlP' :
        \ fname == '__Gundo__' ? 'Gundo' :
        \ fname == '__Gundo_Preview__' ? 'Gundo Preview' :
        \ fname =~ 'NERD_tree' ? 'Files' :
        \ &ft == 'unite' ? 'Unite' :
        \ &ft == 'vimfiler' ? 'VimFiler' :
        \ &ft == 'vimshell' ? 'VimShell' :
        \ winwidth(0) > 60 ? lightline#mode() : ''
endfunction

let g:tagbar_status_func = 'TagbarStatusFunc'

function! TagbarStatusFunc(current, sort, fname, ...) abort
    let g:lightline.fname = a:fname
  return lightline#statusline(0)
endfunction


" Treat .test files as python files by default
au BufNewFile,BufRead *.test,*.stage set ft=python
au BufNewFile,BufRead *.log AnsiEsc
au BufNewFile,BufRead *.log set wrap

" Special filetype settings
set shiftwidth  =4
set softtabstop =4
set tabstop     =4
set expandtab
set autoindent

au FileType json,cpp,c,js,jsx,javascript,typescript,tsx,ts,typescriptreact,javascriptreact setl ts=2 sw=2 sts=2
